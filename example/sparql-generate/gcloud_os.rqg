BASE <https://w3id.org/cocoon/data/> 
PREFIX iter: <http://w3id.org/sparql-generate/iter/>
PREFIX fun: <http://w3id.org/sparql-generate/fn/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX gr: <http://purl.org/goodrelations/v1#>
PREFIX cocoon: <https://raw.githubusercontent.com/miranda-zhang/cloud-computing-schema/master/ontology_dev/cocoon.ttl>

GENERATE { 
    <os/glcoud/{?key}> a cocoon:SystemImage;
        rdfs:label ?key;
        cocoon:hasProvider cocoon:gcloud .

    <os/glcoud/{?key}> gr:hasPriceSpecification [ 
        a cocoon:OSPriceSpecification ; 
            gr:hasCurrency "USD"^^xsd:string; 
            gr:hasCurrencyValue "{?low}"^^xsd:double;
            cocoon:forCoresLessEqual "{?cores}"^^xsd:decimal;
            cocoon:chargedPerCore "{?percore}"^^xsd:boolean;
    ] .

    <os/glcoud/{?key}> gr:hasPriceSpecification [ 
        a cocoon:OSPriceSpecification ; 
            gr:hasCurrency "USD"^^xsd:string; 
            gr:hasCurrencyValue "{?high}"^^xsd:double;
            cocoon:forCoresMoreThan "{?cores}"^^xsd:decimal;
            cocoon:chargedPerCore "{?percore}"^^xsd:boolean;
    ] .
}
SOURCE <https://raw.githubusercontent.com/miranda-zhang/cloud-computing-schema/master/example/data/gcloud_os.json> AS ?source
ITERATOR  iter:JSONListKeys(?source) AS ?key
WHERE {
    BIND(fun:JSONPath(?source, '$.{?key}' ) AS ?OSPriceSpec)
    BIND(fun:JSONPath(?OSPriceSpec, '.low' ) AS ?low)
    BIND(fun:JSONPath(?OSPriceSpec, '.high' ) AS ?high)
    BIND(fun:JSONPath(?OSPriceSpec, '.percore' ) AS ?percore)
    BIND(fun:JSONPath(?OSPriceSpec, '.cores' ) AS ?cores)
}
